IF
	ForceMarkedSpell(MARKED_SPELL)
	SetSpellTarget(Nothing)
	Global("Buffed","LOCALS",0)
	See(NearestEnemyOf(Myself),0)
THEN
	RESPONSE #100
		MarkSpellAndObject("21022114",Myself,SPELLCAST_IGNORE_SEE | SPELLCAST_IGNORE_VALID_SPELL_TARGET | SPELLCAST_RANDOM)  // [WIZARD_ARMOR, WIZARD_SHIELD]
		Continue()
END

IF
	!IsMarkedSpell(MARKED_SPELL)
THEN
	RESPONSE #100
		SetGlobal("Buffed","LOCALS",1)
		Spell(SpellTarget,MARKED_SPELL)
		WaitAnimation(Myself,WALK)
		WaitAnimation(Myself,CONJURE)
		WaitAnimation(Myself,CAST)
		ForceMarkedSpell(MARKED_SPELL)
END

IF
	ForceMarkedSpell(MARKED_SPELL)
	SetSpellTarget(Nothing)
	See(NearestEnemyOf(Myself),0)
THEN
	RESPONSE #100
		MarkSpellAndObject("39983999",Myself,SPELLCAST_IGNORE_SEE | SPELLCAST_IGNORE_VALID_SPELL_TARGET | SPELLCAST_IGNORE_HAS_CHECK)  // [INNATE_CHAMELEON_POWER, INNATE_ANIMAL_AFFINITY]
		Continue()
END

IF
	!IsMarkedSpell(MARKED_SPELL)
THEN
	RESPONSE #100
		ForceSpell(SpellTarget,MARKED_SPELL)
		WaitAnimation(Myself,WALK)
		WaitAnimation(Myself,CONJURE)
		WaitAnimation(Myself,CAST)
		ForceMarkedSpell(MARKED_SPELL)
END

IF
	See(NearestEnemyOf(Myself),0)
THEN
	RESPONSE #100
		EquipWeapon()
		AttackOneRound(LastMarkedObject)
END

IF
	Or(8)
		See("Heartless_Dead1",0)
		See("Heartless_Dead2",0)
		See("Heartless_Dead3",0)
		See("Heartless_Dead4",0)
		See("Heartless_Dead5",0)
		See("Heartless_Dead6",0)
		See("Heartless_Dead7",0)
		See("Heartless_Dead8",0)
THEN
	RESPONSE #100
		SetGlobal("Fleeing","LOCALS",1)
		RunAwayFrom(LastMarkedObject,45)
END

IF
	Global("Fleeing","LOCALS",1)
	!See("Heartless_Dead1",0)
	!See("Heartless_Dead2",0)
	!See("Heartless_Dead3",0)
	!See("Heartless_Dead4",0)
	!See("Heartless_Dead5",0)
	!See("Heartless_Dead6",0)
	!See("Heartless_Dead7",0)
	!See("Heartless_Dead8",0)
THEN
	RESPONSE #100
		ReturnToSavedLocation(0)
		SetGlobal("Fleeing","LOCALS",0)
END


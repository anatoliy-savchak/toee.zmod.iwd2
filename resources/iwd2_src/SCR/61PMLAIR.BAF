IF
	Global("PlayerSawChest","MYAREA",0)
	See([PC],0)
THEN
	RESPONSE #100
		AddJournalEntry(36569)  // We saw a lone chest on a small island in Dragon's Eye's magma sea, but no obvious way to get to it.
		SetGlobal("PlayerSawChest","MYAREA",1)
END

IF
	Global("PlayerKnowsStash","MYAREA",0)
	See([PC],0)
	See([ENEMY.MONSTER],0)
	IsAnimationID(LastMarkedObject,Imp)
THEN
	RESPONSE #100
		AddJournalEntry(36570)  // It appears lava mephits have built a lair on the island containing the chest we saw.
		SetGlobal("PlayerKnowsStash","MYAREA",1)
END

IF
	!Global("CurrentDestination","MYAREA",7)
	IsOverMe([ANYONE])
	Dead(LastTrigger)
THEN
	RESPONSE #100
		ActionOverride(LastTrigger,DestroySelf())
END

IF
	Global("MephitLairSpawn","MYAREA",1)
THEN
	RESPONSE #100
		CreateCreature("61mepLav","",[-1.-1],6)  // Lava Mephit
		SetGlobal("MephitLairOccupied","MYAREA",1)
		SetGlobal("MephitLairSpawn","MYAREA",0)
END

IF
	GlobalLT("61CurrentDay","GLOBAL",2)
	Global("MephitLairOccupied","MYAREA",1)
	!IsOverMe([ANYONE])
THEN
	RESPONSE #100
		SetGlobal("MephitLairOccupied","MYAREA",0)
END

IF
	GlobalLT("61CurrentDay","GLOBAL",2)
	Global("MephitLairOccupied","MYAREA",0)
	IsOverMe([ANYONE])
THEN
	RESPONSE #100
		SetGlobal("MephitLairOccupied","MYAREA",1)
END

